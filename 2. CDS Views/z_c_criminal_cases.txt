@EndUserText.label: 'CDS - Criminal Cases'
@AccessControl.authorizationCheck: #NOT_REQUIRED
@Metadata.allowExtensions: true
define view entity z_c_criminal_cases
  as select from zcrime_records as crecords
  association        to parent z_c_criminal as _criminal  on $projection.CriminalId = _criminal.CriminalId
  association [0..1] to Z_C_COMPLAINT       as _complaint on $projection.ComplaintId = _complaint.ComplaintId
  association [0..1] to z_c_crime           as _crime     on $projection.CrimeId = _crime.CrimeId
  association [0..1] to z_c_court           as _court     on $projection.CourtId = _court.CourtId
  association [0..1] to z_c_police          as _police    on $projection.PoliceId = _police.PoliceId

{
      @EndUserText.label: 'Case ID'
      @Consumption.valueHelpDefinition: [{ entity : {name: 'Z_C_CRIME_RECORDS', element: 'CaseId'  } }]
  key case_id                                          as CaseId,
      @EndUserText.label: 'Criminal ID'
      @Consumption.valueHelpDefinition: [{ entity : {name: 'Z_C_CRIMINAL', element: 'CriminalId'  } }]
  key criminal_id                                      as CriminalId,
      @EndUserText.label: 'Complaint ID'
      @Consumption.valueHelpDefinition: [{ entity : {name: 'Z_C_COMPLAINT', element: 'ComplaintId'  } }]
      compliant_id                                     as ComplaintId,
      @ObjectModel.text.element: ['CRemarks']
      @EndUserText.label: 'Crime Name'
      @Consumption.valueHelpDefinition: [{ entity : {name: 'Z_C_CRIME', element: 'CrimeId'  } }]
      crime_id                                         as CrimeId,
      @EndUserText.label: 'Crime Type'
      @ObjectModel.text.element: ['CRemarks']
      concat_with_space( _crime.CrimeId,
      concat_with_space( '-', _crime.Remarks, 1 ), 1 ) as CType,
      @EndUserText.label: 'Crime Type'
      _crime.Name                                      as CRemarks,
      @ObjectModel.text.element: ['CourtName']
      @EndUserText.label: 'Court Name'
      @Consumption.valueHelpDefinition: [{ entity : {name: 'Z_C_COURT', element: 'CourtId'  } }]
      court_id                                         as CourtId,
      @Consumption.valueHelpDefinition: [{ entity : {name: 'Z_C_COURT', element: 'Name'  } }]
      @EndUserText.label: 'Court Name'
      _court.Name                                      as CourtName,
      @EndUserText.label: 'Police Name'
      @ObjectModel.text.element: ['PoliceName']
      @Consumption.valueHelpDefinition: [{ entity : {name: 'Z_C_POLICE', element: 'PoliceId'  } }]
      police_id                                        as PoliceId,
      @EndUserText.label: 'Police Name'
      @Consumption.valueHelpDefinition: [{ entity : {name: 'Z_C_POLICE', element: 'Name'  } }]
      _police.Name                                     as PoliceName,
      @EndUserText.label: 'Case Details'
      remarks                                          as Remarks,
      @Semantics.systemDateTime.lastChangedAt: true
      last_changed_at                                  as LastChangedAt,
      @Semantics.systemDateTime.localInstanceLastChangedAt: true
      local_last_changed_at                            as LocalLastChangedAt,
      _complaint,
      _criminal, // Make association public
      _crime,
      _court,
      _police
}
